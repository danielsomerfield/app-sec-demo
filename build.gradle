import groovy.sql.Sql

import java.security.MessageDigest

apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'com.bmuschko.tomcat'

sourceCompatibility = 1.8
version = '1.0'

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.bmuschko:gradle-tomcat-plugin:2.0'
    }
}

repositories {
    mavenCentral()
}

configurations { driver }

dependencies {

    def springVersion="4.0.1.RELEASE"
    def springSecurityVersion="3.2.5.RELEASE"
    def tomcatVersion="7.0.50"

    compile "org.springframework:spring-webmvc:${springVersion}"
    compile "org.springframework:spring-jdbc:${springVersion}"
    compile "org.springframework.security:spring-security-web:${springSecurityVersion}"
    compile "org.springframework.security:spring-security-config:${springSecurityVersion}"
    compile "com.fasterxml.jackson.core:jackson-core:2.3.0"
    compile "com.fasterxml.jackson.core:jackson-databind:2.3.0"

    compile 'com.h2database:h2:1.4.182'
    compile 'javax.servlet:jstl:1.2'
    providedCompile "javax.servlet:javax.servlet-api:3.1.0"
    testCompile group: 'junit', name: 'junit', version: '4.11'
    driver 'com.h2database:h2:1.4.182'

    tomcat "org.apache.tomcat.embed:tomcat-embed-core:${tomcatVersion}",
            "org.apache.tomcat.embed:tomcat-embed-logging-juli:${tomcatVersion}"
    tomcat("org.apache.tomcat.embed:tomcat-embed-jasper:${tomcatVersion}") {
        exclude group: 'org.eclipse.jdt.core.compiler', module: 'ecj'
    }
}

URLClassLoader loader = GroovyObject.class.classLoader
configurations.driver.each { File file ->
    loader.addURL(file.toURI().toURL())
}

def buildTables() {
    runSqlStatements(
            [
                    '''CREATE TABLE IF NOT EXISTS directory_entry (
                        id BIGINT PRIMARY KEY AUTO_INCREMENT,
                        first_name  VARCHAR(255) NOT NULL,
                        last_name   VARCHAR(255) NOT NULL
                    )''',

                    '''CREATE UNIQUE INDEX IF NOT EXISTS directory_entry_idx ON directory_entry(first_name, last_name)''',

                    '''CREATE TABLE IF NOT EXISTS application_user (
                        id int primary key,
                        username        VARCHAR(255) NOT NULL,
                        password_hash   VARCHAR(255) NOT NULL
                    )'''
            ]
    )
}

def dropTables() {
    runSqlStatements('DROP TABLE IF EXISTS directory_entry')
    runSqlStatements('DROP TABLE IF EXISTS application_user')
}

task rebuildDatabase << {
    dropTables()
    buildTables()
    doInserts()
}

def doInserts() {
    int x = 0;
    def idGenerator = {
        x++
    }
    runSqlStatements(
            [
                    insertEntry('Daniel', 'Somerfield', idGenerator),
                    insertEntry('Bob', 'Newhart', idGenerator),
                    insertEntry('Richard', 'Stallman', idGenerator),
                    insertEntry('George', 'Lucas', idGenerator),
                    insertEntry('Ronald', 'Reagan', idGenerator),
                    insertEntry('Tim', 'Conway', idGenerator),
                    insertEntry('Richard', 'Marks', idGenerator),
                    insertEntry('Vladamir', 'Proscule', idGenerator),
                    insertEntry('Micky', 'Rourke', idGenerator),
                    insertEntry('Sebastian', 'Mariner', idGenerator),
                    insertEntry('Thomas', 'Beckett', idGenerator),
                    insertEntry('Ricky', 'Astley', idGenerator),
                    insertUser('daniel', 'mypassword', idGenerator),
                    insertUser('admin', 'admin', idGenerator),
                    insertUser('admin1', 'admin', idGenerator)
            ]
    )
}

def insertUser(username, password, idGenerator) {
    return String.format("INSERT INTO application_user (id, username, password_hash) VALUES (%s, '%s', '%s')",
            idGenerator(), username, hashPassword(password))
}

def hashPassword(password) {
    MessageDigest m = MessageDigest.getInstance("MD5")
    m.update(password.getBytes("UTF-8"))
    toHex(m.digest())
}

def toHex(bytes) {
    StringBuffer sb = new StringBuffer();
    for (int i = 0; i < bytes.length; i++) {
        if ((0xff & bytes[i]) < 0x10) {
            sb.append('0');
        }
        sb.append(Integer.toHexString(0xff & bytes[i]));
    }
    return sb.toString();
}

def insertEntry(firstname, lastname, idGenerator) {
    return String.format("INSERT INTO directory_entry (id, first_name, last_name) VALUES (%s, '%s', '%s')", idGenerator(), firstname, lastname)
}

def runSqlFunctions(sqlFunction) {
    def instance = Sql.newInstance("jdbc:h2:~/appsecdemo", "", "", "org.h2.Driver")
    try {
        sqlFunction(instance)
    } finally {
        instance.close()
    }

}

def runSqlStatements(sql) {
    if (sql instanceof String) {
        runSqlFunctions { conn -> conn.execute(sql) }
    } else {
        sql.each { s -> runSqlStatements(s) }
    }
}

tomcatRunWar.dependsOn rebuildDatabase
tomcatRun.dependsOn rebuildDatabase

